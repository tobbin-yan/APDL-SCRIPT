## K6/K8网壳

```
/clear
/prep7

*afun,deg
csys,2

span = 100
rise = 25

r = ((span/2)**2)/2/rise + rise/2
alpha = asin(span/2/r)

nc = 4
k = 6

*dim,kpa,array,nc,nc*k,1

kid = 1

k,kid,r,0,90
kid = kid+1

dalpha = alpha/nc
```
计算基本几何数据
```
*do,i,1,nc
  *do ,j,1,i*k
    k,kid,r,(j-1)*360/k/i,90-i*dalpha
    kpa(i,j) = kid
    kid = kid+1
  *enddo
*enddo
```
布点

```
csys,0

*do,i,1,nc
  *do,j,1,i*k-1
    l,kpa(i,j),kpa(i,j+1)
  *enddo
  l,kpa(i,i*k),kpa(i,1)
*enddo
```
环
```
*do,i,1,k
  l,1,kpa(1,i)
*enddo
```
第一圈径向
```
*do,i,1,k
  *do,j,1,nc-1
    l,kpa(j,i+(i-1)*(j-1)),kpa(j+1,i+(i-1)*j)
  *enddo
*enddo
```
分扇区（前k-1）个扇区
```
*dim,fa,array,nc,nc+1,k

*do,i,1,k-1
  *do,j,1,nc
    *do,m,1,j+1
      fa(j,m,i) = kpa(j,j*(i-1)+m)
    *enddo
  *enddo
*enddo
```
最后一个扇区
```
i=k
*do,j,1,nc
  *do,m,1,j
    fa(j,m,i) = kpa(j,j*(i-1)+m)
  *enddo
*enddo
*do,i,1,nc
  fa(i,i+1,k) = fa(i,1,1)
*enddo
```
所有斜杆
```
*do,i,1,k
  *do,j,2,nc
    *do,m,2,j
      l,fa(j,m,i),fa(j-1,m-1,i)
      l,fa(j,m,i),fa(j-1,m,i)
    *enddo
  *enddo
*enddo
```
材料，截面和网格
```
et,1,beam188

mp,ex,1,2e11
mp,prxy,1,.3
mp,dens,1,7850

sectype,1,beam,i,i200*200
secdata,.2,.2,.2,.012,.012,.008

k,1000,0,0,0

latt,1,,1,,1000,,1
lesize,1
lmesh,all
```
约束和求解
```
hoc = r-rise
! height of outest ring

ksel,s,loc,z,hoc
dk,all,ux,0,0,,uy,uz
allsel,all

/solu
antype,modal
modopt,lanb,12

solve
```